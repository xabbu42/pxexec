#!/usr/bin/env perl 
use strict;
use warnings;

my $verbose = grep /^(-v|--verbose)$/, @ARGV;

open my $h, '-|', 'xprop', '-root', '-spy'
	or die "Could not listen to property changes using xprop: $!";
while (<$h>) {
	if (/^PXEXEC_CMD_([\w.-]+)\(STRING\)\s*=\s*(.*)$/) {
		my ($id, $cmd) = ($1, eval $2);
		system('xprop', '-root', '-remove', 'PXEXEC_CMD_' . $id);
		if ($cmd) {
			warn "$id: $cmd" if $verbose;
			my $output = `$cmd` // "";
			my $res;
			if ($? == -1) {
				$res = "Can't exec \"$cmd\": $!";
			} elsif ($? & 127) {
				$res = "Child died with signal " . ($? & 127);
			} else {
				$res = ($? >> 8) . "\t" . $output;
			}
			my $prop = 'PXEXEC_RESULT_' . $id;
			system('xprop', '-f', $prop, '8s', '-root', '-set', $prop, $res);
		} else {
			warn "Could not parse line '$_'" unless $cmd;
		}
	}
}

