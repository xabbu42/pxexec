#!/usr/bin/env perl

=head1 NAME

pxexec - execute commands over X11 protocol

=head1 SYNOPSIS

pxexec [options] command

execute commands over X11 protocol

=head1 OPTIONS

 --help|-h   brief help message

=cut

use strict;
use warnings;

use Pxexec;

use Getopt::Long;
use Pod::Usage;
use Data::Uniqid qw/suniqid/;
use String::ShellQuote qw/shell_quote/;

my ($help);
GetOptions(
	"help|h"    => \$help,
	) or pod2usage(-exitval => 2, -verbose => 1);
pod2usage(-verbose => 1) if $help;

my $ver = get_property('PXEXEC_VERSION');
die "No pxlisten process found! You need to run pxlisten somewhere."
	unless $ver;
die "Wrong pxlisten version."
	unless $ver eq "0.1";
my $cmd = join ' ', shell_quote @ARGV;
my $host = `uname -n`;
chomp $host;
my $id = $host . '_' . suniqid;

my ($prop, $res) = wait_property(
	'PXEXEC_RESULT_' . $id, 'delete',
	sub { set_property('PXEXEC_CMD_' . $id, $cmd); },
);

die $res unless defined $res && $res =~ /\t/;
my ($exitcode, $output) = split "\t", $res, 2;
print $output;
exit $exitcode;
